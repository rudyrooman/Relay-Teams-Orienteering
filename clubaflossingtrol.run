# gebruik de macro in ms excel om de inschrijvingen te verwerken en
# snelheden te linken aan lopers.
# het resultaat is de data file/ 

reset;
model '/home/rudy/Dropbox/OL/Aflossingen/clubaflossing/clubaflossingtrol.mod';
data '/home/rudy/Dropbox/OL/Aflossingen/clubaflossing/clubaflossing.dat';

option solver gurobi;

# change display settings
option omit_zero_rows 1;
option display_1col 30;
option solution_round 10;
#option solver_msg 0;

let max_aantal_teams := card(runner) div 6;
printf '\nEr zijn maximaal %s complete ploegen.\n', max_aantal_teams;
printf 'Er zijn %s competitieve ploegen.\n\n',comp_team;

let aantalteams := 1;

# eerst maken we de competieve teams
# aantal comp_teams wordt opgegeven in de data
for {1..comp_team} {
		solve;
		# tonen competitieve ploeg
		printf '\nTROL%s\n',aantalteams;
		printf { t in team, k in omloop,r in runner: match[t,r,k]=1 and t =aantalteams} '%s %s %-18s %s\n',k,afstand[k],r,points[r];
		printf  {t in team:t = aantalteams} 'puntentotaal%13s\n',sum {r in runner, k in omloop: match[t,r,k]=1} points[r] ;
		printf  {t in team:t = aantalteams} 'tijd%24.1f min.\n\n',sum {j in runner, k in omloop } match[t,j,k]*speed[j]*afstand[k] ;
		# vastleggen competitieve ploeg
		fix { r in runner, t in team, k in omloop: match[t,r,k]=1 and t = aantalteams} match[t,r,k];
		let aantalteams := aantalteams+1;
		}

# nu maken we de resterende teams
let aantalteams := max_aantal_teams;
solve;

# toon alle lopers die een ploeg hebben
print;
for {t in team:t > comp_team} {
	printf '\nTROL%s\n',t;
	printf {k in omloop,r in runner : match[t,r,k]=1 } '%s %s %-18s%s\n',k,afstand[k],r,points[r];
	fix { r in runner, k in omloop: match[t,r,k]=1 } match[t,r,k]; 
	printf 'puntentotaal%12s\n',sum {r in runner, k in omloop: match[t,r,k]=1} points[r] ;
	printf  'tijd %22.1f min.\n\n',sum {j in runner, k in omloop } match[t,j,k]*speed[j]*afstand[k] ;
		
	}	
print;


# de lopers die overblijven
for {r in runner} if sum { t in team, k in omloop} match[t,r,k]= 0 then printf '%-18s %s heeft geen ploeg.\n',r,points[r] ;

# set back to original display settings
option omit_zero_rows 0;
option display_1col 20;
option solver_msg 1;